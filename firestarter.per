
; Conditions to remember:
; High starting resources
; High population limit (500)
; Should build villagers and militia in parallel and attack!

; ----- Constants -------------------------------------------------------------

(defconst gv-yes 1)
(defconst gv-no 0)

(defconst gv-dark-age 0)
(defconst gv-advancing-to-feudal 1)
(defconst gv-feudal-age 2)
(defconst gv-advancing-to-castle 3)
(defconst gv-castle-age 4)
(defconst gv-advancing-to-imperial 5)
(defconst gv-imperial-age 6)

(defconst max-num-dark-villagers 22)

(defconst scouting-timer 3)

(defconst goal-dark-rush 1)
(defconst goal-max-militia-reached 2)
(defconst goal-age-status 3)
(defconst goal-flood-spearmen 4)

(defconst start-dark-rush-militia-count 5)
(defconst end-dark-rush-militia-count 15)

(defconst gv-dr-building 1) ; dark rush states
(defconst gv-dr-rushing 2)
(defconst gv-dr-finished 3)

#load-if-defined UP-POCKET-POSITION
(defconst scouting-type position-opposite)
#else
(defconst scouting-type position-flank)
#end-if

; Initial state
(defrule
(true)
=>
(set-goal goal-dark-rush gv-dr-building)
(set-goal goal-max-militia-reached gv-no)
(set-goal goal-age-status gv-dark-age)
(set-goal goal-flood-spearmen gv-no)
(disable-self))

; -----------------------------------------------------------------------------

; ----- Strategic Numbers -----------------------------------------------------

(defrule
(true)
=>
(set-strategic-number sn-enable-patrol-attack 1)
; [OFF] (set-strategic-number sn-cap-civilian-builders 100)
(disable-self))

(defrule
(true)
=>
(set-strategic-number sn-maximum-food-drop-distance 15)
(set-strategic-number sn-maximum-wood-drop-distance 15)
(set-strategic-number sn-maximum-stone-drop-distance 25)
(set-strategic-number sn-maximum-gold-drop-distance 25)
(set-strategic-number sn-mill-max-distance 15)
(set-strategic-number sn-maximum-hunt-drop-distance 35)
(set-strategic-number sn-enable-boar-hunting 2)  ; 1 = boar, deer; 2 = boar only
(disable-self))

(defrule
(true)
=>
(set-strategic-number sn-percent-civilian-explorers 100)
(set-strategic-number sn-minimum-civilian-explorers 100)
(set-strategic-number sn-cap-civilian-explorers 100)
(set-strategic-number sn-total-number-explorers 101)
(set-strategic-number sn-number-explore-groups 101)
(set-strategic-number sn-minimum-explore-group-size 1)
(set-strategic-number sn-maximum-explore-group-size 1)
(set-strategic-number sn-initial-exploration-required 0)
(set-strategic-number sn-percent-attack-soldiers 100)
(set-strategic-number sn-blot-exploration-map 0)
(set-strategic-number sn-home-exploration-time 0)
(disable-self))

(defrule
(resource-found food)
=>
(set-strategic-number sn-percent-civilian-explorers 0)
(set-strategic-number sn-minimum-civilian-explorers 0)
(set-strategic-number sn-cap-civilian-explorers 0)
(set-strategic-number sn-total-number-explorers 1)
(set-strategic-number sn-number-explore-groups 1)
(disable-self))

(defrule
(unit-type-count-total villager >= 10)
=>
(set-strategic-number sn-minimum-number-hunters 5)
(chat-local-to-self "Go slay the boar!")
(disable-self))

(defrule
(true)
=>
(up-send-scout group-type-land-explore scouting-type)
(enable-timer scouting-timer 5)
(disable-self))

(defrule
(timer-triggered scouting-timer)
(game-time < 300)
(players-building-count every-enemy <= 0)
(military-population >= 1)
=>
(up-send-scout group-type-land-explore scouting-type)
(enable-timer scouting-timer 5));

; -----------------------------------------------------------------------------

; ----- Age Advancement -------------------------------------------------------

(defrule
(goal goal-dark-rush gv-dr-finished)
(can-research feudal-age)
=>
(set-goal goal-age-status gv-advancing-to-feudal)
(research feudal-age)
(chat-local-to-self "Heading to Feudal!"))

(defrule
(current-age == feudal-age)
=>
(set-goal goal-age-status gv-feudal-age)
(disable-self))

(defrule
(can-research castle-age)
=>
(research castle-age)
(set-goal goal-age-status gv-advancing-to-castle)
(chat-local-to-self "Heading to Castle!"))

(defrule
(current-age == castle-age)
=>
(set-goal goal-age-status gv-castle-age)
(disable-self))

(defrule
(can-research imperial-age)
=>
(research imperial-age)
(set-goal goal-age-status gv-advancing-to-imperial)
(chat-local-to-self "Heading to Imperial!!"))

(defrule
(current-age == imperial-age)
=>
(set-goal goal-age-status gv-imperial-age)
(disable-self))

; -----------------------------------------------------------------------------

; ----- Economy ---------------------------------------------------------------

(defrule
(goal goal-age-status gv-dark-age)
=>
; Unit heavy in dark age, prioritize food
(set-strategic-number sn-food-gatherer-percentage 90)
(set-strategic-number sn-wood-gatherer-percentage 10)
(set-strategic-number sn-gold-gatherer-percentage 0)
(set-strategic-number sn-stone-gatherer-percentage 0))

(defrule
(goal goal-age-status gv-advancing-to-feudal)
=>
(set-strategic-number sn-food-gatherer-percentage 40)
(set-strategic-number sn-wood-gatherer-percentage 50)
(set-strategic-number sn-gold-gatherer-percentage 10)
(set-strategic-number sn-stone-gatherer-percentage 0))

(defrule
(goal goal-age-status gv-feudal-age)
(building-type-count-total blacksmith > 0)
(building-type-count-total market > 0)
=>
(set-strategic-number sn-food-gatherer-percentage 55)
(set-strategic-number sn-wood-gatherer-percentage 30)
(set-strategic-number sn-gold-gatherer-percentage 10)
(set-strategic-number sn-stone-gatherer-percentage 5))

(defrule
(current-age == castle-age)
=>
(set-strategic-number sn-food-gatherer-percentage 50)
(set-strategic-number sn-wood-gatherer-percentage 30)
(set-strategic-number sn-gold-gatherer-percentage 10)
(set-strategic-number sn-stone-gatherer-percentage 10))

(defrule
(current-age == imperial-age)
=>
(set-strategic-number sn-food-gatherer-percentage 45)
(set-strategic-number sn-wood-gatherer-percentage 30)
(set-strategic-number sn-gold-gatherer-percentage 15)
(set-strategic-number sn-stone-gatherer-percentage 10))

; -----------------------------------------------------------------------------

; ----- Dark Age Rush ---------------------------------------------------------

(defrule
(goal goal-dark-rush gv-dr-building)
(unit-type-count-total militiaman-line > start-dark-rush-militia-count)
(not    (players-building-count every-enemy <= 0))
(game-time < 480) ; after 8 min, don't bother
=>
(set-goal goal-dark-rush gv-dr-rushing)
(chat-local-to-self "Rushing!"))

(defrule
(goal goal-dark-rush gv-dr-building)
(game-time > 480)
=>
(set-goal goal-dark-rush gv-dr-finished)
(chat-local-to-self "Skipping the rush, missed chance."))

(defrule
(goal goal-dark-rush gv-dr-rushing)
(game-time > 600)
=>
(set-goal goal-dark-rush gv-dr-finished)
(chat-local-to-self "Finished rushing."))

(defrule
(goal goal-dark-rush gv-dr-finished)
=>
(set-strategic-number sn-maximum-hunt-drop-distance 15)
; [OFF] (set-strategic-number sn-enable-boar-hunting 0)
(chat-local-to-self "Dropping hunt distance. The boar hunt is over for now.")
(disable-self))

; -----------------------------------------------------------------------------

; ----- Tech ------------------------------------------------------------------

; ----- Dark Age ----------------------
(defrule
(food-amount < 50)
(can-research ri-loom)
=>
(research ri-loom)
(chat-local-to-self "Food low. Researching loom."))

; ----- Feudal Age --------------------

(defrule
(can-research ri-wheel-barrow)
=>
(research ri-wheel-barrow)
(chat-local-to-self "Researching wheel barrow."))

; ----- Imperial Age ------------------

(defrule
(can-research ri-murder-holes)
=>
(research ri-murder-holes)
(chat-local-to-self "Researching murder holes."))

; -----------------------------------------------------------------------------

; ----- Buildings -------------------------------------------------------------

; ASAP
(defrule
(building-type-count-total barracks < 2)
(can-build barracks)
=>
(build barracks))

(defrule
(or (goal goal-dark-rush gv-dr-building)
    (goal goal-dark-rush gv-dr-rushing))
(can-build house)
(housing-headroom < 7)
(population-headroom > 3)
=>
(build house))

(defrule
(goal goal-dark-rush gv-dr-finished)
(can-build house)
(housing-headroom < 4)
(population-headroom > 3)
=>
(build house))

(defrule
(building-type-count-total house > 0)
(building-type-count-total mill == 0)
(resource-found food)
(can-build mill)
=>
(build mill))

(defrule
(building-type-count-total mill > 0)
(building-type-count-total lumber-camp == 0)
(resource-found wood)
(can-build lumber-camp)
=>
(build lumber-camp))

(defrule
(or (goal goal-age-status gv-dark-age)
    (goal goal-age-status gv-advancing-to-feudal))
(goal goal-dark-rush gv-dr-finished)
(building-type-count-total mill > 0)
(building-type-count-total lumber-camp > 0)
(or (building-type-count-total farm < 15)
    (idle-farm-count == 0))
(can-build farm)
=>
(up-assign-builders c: farm c: 2)
(build farm))

(defrule
(current-age > dark-age)
(idle-farm-count == 0)
(can-build farm)
=>
(build farm))

(defrule
(building-type-count-total lumber-camp > 0)
(strategic-number sn-camp-max-distance < 35)
=>
(set-strategic-number sn-camp-max-distance 35)
(chat-local-to-self "Camp distance 35.")
(disable-self))

(defrule
(or (current-age > dark-age)
    (goal goal-age-status gv-advancing-to-feudal))
(resource-found gold)
(building-type-count mining-camp < 4)
(cc-players-unit-type-count 0 66 > 0)
(dropsite-min-distance gold > 5)
(dropsite-min-distance gold < 255)
(can-build mining-camp)
=>
(build mining-camp))

; ----- Feudal ------------------------

(defrule
(current-age >= feudal-age)
(building-type-count-total blacksmith < 1)
(can-build blacksmith)
=>
(build blacksmith))

(defrule
(current-age == feudal-age)
(building-type-count-total market < 1)
(can-build market)
=>
(build market))

(defrule
(current-age >= feudal-age)
(building-type-count-total blacksmith > 0)
(building-type-count-total market > 0)
(resource-found stone)
(dropsite-min-distance stone > 5)
(dropsite-min-distance stone < 255)
(building-type-count mining-camp < 4)
(can-build mining-camp)
=>
(build mining-camp))

; ----- Castle ------------------------

(defrule
(can-build castle)
=>
(build castle))

(defrule
(building-type-count-total town-center < 6)
(can-build town-center)
=>
(build town-center))

(defrule
(or (current-age == castle-age)
    (current-age == imperial-age))
(can-build house)
(housing-headroom < 10)
(population-headroom > 3)
=>
(build house))

; ----- Imperial ----------------------

(defrule
(building-type-count-total university < 1)
(can-build university)
=>
(build university))

(defrule
(goal goal-flood-spearmen gv-yes)
(building-type-count-total barracks < 10)
(can-build barracks)
=>
(build barracks))

; -----------------------------------------------------------------------------

; ----- Unit training ---------------------------------------------------------

(defrule
(current-age == dark-age)
(unit-type-count-total villager < max-num-dark-villagers)
(can-train villager)
=>
(train villager))

; constantly train villagers
(defrule
(not (current-age == dark-age))
(can-train villager)
=>
(train villager))

(defrule
(or (goal goal-dark-rush gv-dr-building)
    (goal goal-dark-rush gv-dr-rushing))
(unit-type-count-total militiaman-line >= end-dark-rush-militia-count)
=>
(set-goal goal-max-militia-reached gv-yes)
(chat-local-to-self "Done training rush militia.")
(disable-self))

(defrule
(or (goal goal-dark-rush gv-dr-building)
    (goal goal-dark-rush gv-dr-rushing))
(goal goal-max-militia-reached gv-no)
(can-train militiaman-line)
=>
(train militiaman-line))

; -------------------------------------

(defrule
(goal goal-flood-spearmen gv-yes)
(can-train spearman-line)
=>
(train spearman-line))

; -----------------------------------------------------------------------------

; ----- Attack rules ----------------------------------------------------------

; re-issue the attack now call every minute

(defrule
(goal goal-dark-rush gv-dr-rushing)
=>
(enable-timer 1 1)
(disable-self))

(defrule
(timer-triggered 1)
(goal goal-dark-rush gv-dr-rushing)
=>
(attack-now)
(disable-timer 1)
(enable-timer 1 60)
(chat-local-to-self "Attack now!"))

(defrule
(or (current-age == castle-age)
    (current-age == imperial-age))
(or (town-under-attack)
    (civilian-population > 100))
=>
(set-goal goal-flood-spearmen gv-yes)
(chat-local-to-self "Let the flood waters gather!"))

; -----------------------------------------------------------------------------