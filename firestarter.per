; Firestarter
; Stevenson's Bane

; Based heavily on scripts by BearTheGreat, Promiskuitiv, and Resonance22

; Conditions to remember:
; High starting resources
; High population limit (500)
; Should build villagers and militia in parallel and attack!

; Scripting notes
;
; Use (up-chat-local-to-self "{string}" c: 0) to display debug text since it will appear in replays as well.
; "c: 0" is added to satisfy the parser for cases where no input is needed.

; ----- Constants -------------------------------------------------------------

(defconst gv-yes 1)
(defconst gv-no 0)

(defconst gv-dark-age 0)
(defconst gv-advancing-to-feudal 1)
(defconst gv-feudal-age 2)
(defconst gv-advancing-to-castle 3)
(defconst gv-castle-age 4)
(defconst gv-advancing-to-imperial 5)
(defconst gv-imperial-age 6)

(defconst dark-age-rush-max-villagers 22)
(defconst feudal-advance-min-villagers 25)
(defconst max-town-size 60)
(defconst max-dark-rush-barracks 3)

(defconst gv-army-status-idle 1)
(defconst gv-army-status-attacking 2)
(defconst gv-army-status-defending 3)

(defconst gv-army-comp-offensive 1)
(defconst gv-army-comp-defensive 2)

(defconst dark-rush-timer 2)
(defconst scouting-timer 3)
(defconst generic-defend-timer 4)
(defconst increase-town-size-timer 5)
(defconst dark-rush-forward-build-timer 6)

(defconst goal-dark-rush 1)
(defconst goal-max-militia-reached 2)
(defconst goal-age-status 3)
(defconst goal-flood-spearmen 4)
(defconst goal-army-status 6)
(defconst goal-army-composition 7)
(defconst goal-tsa 8)
(defconst goal-ri-queued-blacksmith 9)
(defconst goal-dark-rush-forward-build 10)
(defconst goal-ri-queued-university 11)

; These are evaluated only once, in game-constants.per

(defconst gc-is-incas 80)
(defconst gc-is-indians 81)

(defconst gc-civ-has-knights 100)
(defconst gc-civ-has-bombard-cannons 101)

(defconst gc-some-enemy-is-ai 102)
(defconst gc-every-enemy-is-ai 103)

; ; ; ; ; ; ; ; ; ; ;

(defconst start-dark-rush-militia-count 5)
(defconst end-dark-rush-militia-count 15)

(defconst gv-dr-building 1) ; dark rush states
(defconst gv-dr-rushing 2)
(defconst gv-dr-finished 3)
(defconst gv-dr-disabled 4)

(defconst gv-drbf-not-started 1) ; dark rush forward build states
(defconst gv-drbf-building 2)
(defconst gv-drbf-built 3)
(defconst gv-drbf-finished 4)

#load-if-defined UP-POCKET-POSITION
(defconst scouting-type position-opposite)
(defconst scout-unlock-time 300)
#else
(defconst scouting-type position-flank)
(defconst scout-unlock-time 180)
#end-if

(load "gameconstants")

; Initial state
(defrule
(true)
=>
(set-goal goal-age-status gv-dark-age)
(set-goal goal-flood-spearmen gv-no)
(set-goal goal-army-status gv-army-status-idle)
(set-goal goal-army-composition gv-army-comp-defensive)
(set-goal goal-tsa gv-no)
(set-goal goal-ri-queued-blacksmith gv-no)
(set-goal goal-dark-rush-forward-build gv-drbf-not-started)
(set-goal goal-max-militia-reached gv-no)
(disable-self))

(defrule
(goal gc-every-enemy-is-ai gv-no)
=>
(set-goal goal-dark-rush gv-dr-building)
(up-chat-data-to-self "Will attempt dark age rush since at least one enemy is human." c: 0)
(disable-self))

(defrule
(goal gc-every-enemy-is-ai gv-yes)
=>
(set-goal goal-dark-rush gv-dr-disabled)
(up-chat-data-to-self "Skipping dark age rush since every enemy is AI." c: 0)
(disable-self))

; -----------------------------------------------------------------------------

; ----- Strategic Numbers -----------------------------------------------------

(defrule
(true)
=>
(set-strategic-number sn-enable-patrol-attack 1)
(set-strategic-number sn-enable-new-building-system 1)
(set-strategic-number sn-cap-civilian-builders 4)
; high value => siege vs buildings, soldier vs soldier
(set-strategic-number sn-target-evaluation-siege-weapon 500)
(set-strategic-number sn-wall-targeting-mode 1)
(disable-self))

(defrule
(true)
=>
(set-strategic-number sn-maximum-food-drop-distance 15)
(set-strategic-number sn-maximum-wood-drop-distance 15)
(set-strategic-number sn-maximum-stone-drop-distance 25)
(set-strategic-number sn-maximum-gold-drop-distance 25)
(set-strategic-number sn-mill-max-distance 15)
(set-strategic-number sn-maximum-hunt-drop-distance 35)
(set-strategic-number sn-enable-boar-hunting 2)  ; 1 = boar, deer; 2 = boar only
(disable-self))

(defrule
(true)
=>
(set-strategic-number sn-percent-civilian-explorers 100)
(set-strategic-number sn-minimum-civilian-explorers 100)
(set-strategic-number sn-cap-civilian-explorers 100)
(set-strategic-number sn-total-number-explorers 101)
(set-strategic-number sn-number-explore-groups 101)
(set-strategic-number sn-minimum-explore-group-size 1)
(set-strategic-number sn-maximum-explore-group-size 1)
(set-strategic-number sn-initial-exploration-required 0)
(set-strategic-number sn-percent-attack-soldiers 100)
(set-strategic-number sn-blot-exploration-map 0)
(set-strategic-number sn-home-exploration-time 0)
(disable-self))

(defrule
(resource-found food)
=>
(set-strategic-number sn-percent-civilian-explorers 0)
(set-strategic-number sn-minimum-civilian-explorers 0)
(set-strategic-number sn-cap-civilian-explorers 0)
(set-strategic-number sn-total-number-explorers 5)
(set-strategic-number sn-number-explore-groups 5)
(up-chat-data-to-self "Found food. Dropping villager explorers." c: 0)
(disable-self))

(defrule
(unit-type-count-total villager >= 10)
=>
(set-strategic-number sn-minimum-number-hunters 5)
(up-chat-data-to-self "Number of villagers >= 10. Go slay the boar!" c: 0)
(disable-self))

(defrule
(true)
=>
(up-send-scout group-type-land-explore scouting-type)
(enable-timer scouting-timer 5)
(disable-self))

(defrule
(timer-triggered scouting-timer)
(game-time < scout-unlock-time)
(players-building-count every-enemy <= 0)
(military-population >= 1)
=>
(up-send-scout group-type-land-explore scouting-type)
(enable-timer scouting-timer 5));

(defrule
(players-building-count any-enemy > 0)
=>
(set-strategic-number sn-total-number-explorers 1)
(set-strategic-number sn-number-explore-groups 1)
(up-chat-data-to-self "Found an enemy building. Dropping extra explorers" c: 0)
(disable-self))

; ----- Feudal ------------------------

(defrule
(current-age > dark-age)
=>
(set-strategic-number sn-maximum-hunt-drop-distance 15)
; [OFF] (set-strategic-number sn-enable-boar-hunting 0)
(up-chat-data-to-self "Dropping hunt distance." c: 0)
(disable-self))

; ----- Castle, Imperial --------------

(defrule
(or (current-age == castle-age)
    (current-age == imperial-age))
=>
(set-strategic-number sn-cap-civilian-builders 100))

; -----------------------------------------------------------------------------

; ----- Age Advancement -------------------------------------------------------

(defrule
(or (goal goal-dark-rush gv-dr-disabled)
    (goal goal-dark-rush gv-dr-finished))
(unit-type-count-total villager >= feudal-advance-min-villagers)
(can-research feudal-age)
=>
(set-goal goal-age-status gv-advancing-to-feudal)
(research feudal-age)
(up-chat-data-to-self "Heading to Feudal!" c: 0))

(defrule
(current-age == feudal-age)
=>
(set-goal goal-age-status gv-feudal-age)
(disable-self))

(defrule
(can-research castle-age)
=>
(research castle-age)
(set-goal goal-age-status gv-advancing-to-castle)
(up-chat-data-to-self "Heading to Castle!" c: 0))

(defrule
(current-age == castle-age)
=>
(set-goal goal-age-status gv-castle-age)
(disable-self))

(defrule
(can-research imperial-age)
=>
(research imperial-age)
(set-goal goal-age-status gv-advancing-to-imperial)
(up-chat-data-to-self "Heading to Imperial!!" c: 0))

(defrule
(current-age == imperial-age)
=>
(set-goal goal-age-status gv-imperial-age)
(disable-self))

; -----------------------------------------------------------------------------

; ----- Economy ---------------------------------------------------------------

(defrule
(goal goal-age-status gv-dark-age)
=>
; Unit heavy in dark age, prioritize food
(set-strategic-number sn-food-gatherer-percentage 90)
(set-strategic-number sn-wood-gatherer-percentage 10)
(set-strategic-number sn-gold-gatherer-percentage 0)
(set-strategic-number sn-stone-gatherer-percentage 0))

(defrule
(goal goal-age-status gv-advancing-to-feudal)
=>
(set-strategic-number sn-food-gatherer-percentage 30)
(set-strategic-number sn-wood-gatherer-percentage 60)
(set-strategic-number sn-gold-gatherer-percentage 10)
(set-strategic-number sn-stone-gatherer-percentage 0))

(defrule
(goal goal-age-status gv-feudal-age)
(building-type-count-total blacksmith > 0)
(building-type-count-total market > 0)
=>
(set-strategic-number sn-food-gatherer-percentage 35)
(set-strategic-number sn-wood-gatherer-percentage 50)
(set-strategic-number sn-gold-gatherer-percentage 10)
(set-strategic-number sn-stone-gatherer-percentage 5))

(defrule
(or (goal goal-age-status gv-advancing-to-castle)
    (current-age == castle-age))
=>
(set-strategic-number sn-food-gatherer-percentage 35)
(set-strategic-number sn-wood-gatherer-percentage 45)
(set-strategic-number sn-gold-gatherer-percentage 10)
(set-strategic-number sn-stone-gatherer-percentage 10))

(defrule
(current-age == castle-age)
(building-type-count-total town-center > 3)
=>
(set-strategic-number sn-food-gatherer-percentage 60)
(set-strategic-number sn-wood-gatherer-percentage 20)
(set-strategic-number sn-gold-gatherer-percentage 10)
(set-strategic-number sn-stone-gatherer-percentage 10))

(defrule
(current-age == imperial-age)
=>
(set-strategic-number sn-food-gatherer-percentage 50)
(set-strategic-number sn-wood-gatherer-percentage 25)
(set-strategic-number sn-gold-gatherer-percentage 15)
(set-strategic-number sn-stone-gatherer-percentage 10))

; -----------------------------------------------------------------------------

; ----- Dark Age Rush ---------------------------------------------------------

(defrule
(goal goal-dark-rush gv-dr-building)
(unit-type-count-total militiaman-line > start-dark-rush-militia-count)
(not    (players-building-count every-enemy <= 0))
(game-time < 480) ; after 8 min, don't bother
=>
(set-goal goal-dark-rush gv-dr-rushing)
(enable-timer dark-rush-forward-build-timer 1)
(up-chat-data-to-self "Rushing!" c: 0))

(defrule
(timer-triggered dark-rush-forward-build-timer)
=>
(disable-timer dark-rush-forward-build-timer)
(set-goal goal-dark-rush-forward-build gv-drbf-building)
(up-assign-builders c: barracks c: 4)
(up-chat-data-to-self "Building forward!" c: 0))

(defrule
(goal goal-dark-rush-forward-build gv-drbf-building)
(building-type-count-total barracks < max-dark-rush-barracks)
(can-build barracks)
=>
(build-forward barracks))

(defrule
(goal goal-dark-rush-forward-build gv-drbf-building)
(can-build town-center)
=>
(build-forward town-center))

(defrule
(goal goal-dark-rush-forward-build gv-drbf-building)
(building-type-count barracks == max-dark-rush-barracks) ; built
=>
(set-goal goal-dark-rush-forward-build gv-drbf-built)
(up-assign-builders c: barracks c: 1)
(up-chat-data-to-self "Forward barracks built, continue the rush!" c: 0))

(defrule
(goal goal-dark-rush-forward-build gv-drbf-built)
(building-type-count barracks < max-dark-rush-barracks) ; destroyed
=>
(set-goal goal-dark-rush-forward-build gv-drbf-finished)
(up-chat-data-to-self "Forward barracks destroyed..." c: 0))

(defrule
(goal goal-dark-rush gv-dr-building)
(game-time > 480)
=>
(set-goal goal-dark-rush gv-dr-finished)
(up-chat-data-to-self "Skipping the rush, missed chance." c: 0))

(defrule
(goal goal-dark-rush gv-dr-rushing)
(game-time > 600)
(goal goal-dark-rush-forward-build gv-drbf-finished)
=>
(set-goal goal-dark-rush gv-dr-finished)
(up-chat-data-to-self "Finished rushing since forward barracks was destroyed." c: 0))

(defrule
(goal goal-dark-rush gv-dr-rushing)
(game-time > 720)
=>
(set-goal goal-dark-rush gv-dr-finished)
(set-goal goal-dark-rush-forward-build gv-drbf-finished)
(up-chat-data-to-self "Finished rushing since game time has passed 12 minutes." c: 0))

; -----------------------------------------------------------------------------

; ----- Tech ------------------------------------------------------------------

; ----- Dark Age ----------------------
(defrule
(or (or (food-amount < 50)
        (goal goal-dark-rush-forward-build gv-drbf-building))
    (current-age > dark-age))
(can-research ri-loom)
=>
(research ri-loom)
(up-chat-data-to-self "Researching loom." c: 0))

; ----- Feudal Age --------------------

(defrule
(can-research ri-wheel-barrow)
=>
(research ri-wheel-barrow)
(up-chat-data-to-self "Researching wheel barrow." c: 0))

(defrule
(can-research ri-cartography)
=>
(research ri-cartography)
(up-chat-data-to-self "Researching cartography." c: 0))

; ----- Castle Age --------------------

(defrule
(current-age > feudal-age)
(can-research ri-double-bit-axe)
=>
(research ri-double-bit-axe)
(up-chat-data-to-self "Researching double bit axe." c: 0))

(defrule
(current-age > feudal-age)
(can-research ri-bow-saw)
=>
(research ri-bow-saw)
(up-chat-data-to-self "Researching bow saw." c: 0))

(defrule
(current-age > feudal-age)
(can-research ri-horse-collar)
=>
(research ri-horse-collar)
(up-chat-data-to-self "Researching horse collar." c: 0))

(defrule
(current-age > feudal-age)
(can-research ri-heavy-plow)
=>
(research ri-heavy-plow)
(up-chat-data-to-self "Researching heavy plow." c: 0))

; ----- Imperial Age ------------------

; reset every rule pass
(defrule
(true)
=>
(set-goal goal-ri-queued-blacksmith gv-no)
(set-goal goal-ri-queued-university gv-no))

(defrule
(current-age == imperial-age)
(can-research ri-murder-holes)
(goal goal-ri-queued-university gv-no)
=>
(research ri-murder-holes)
(set-goal goal-ri-queued-university gv-yes)
(up-chat-data-to-self "Researching murder holes." c: 0))

(defrule
(current-age == imperial-age)
(can-research ri-scale-mail)
(goal goal-ri-queued-blacksmith gv-no)
=>
(research ri-scale-mail)
(set-goal goal-ri-queued-blacksmith gv-yes)
(up-chat-data-to-self "Researching scale mail armor." c: 0))

(defrule
(current-age == imperial-age)
(can-research ri-forging)
(goal goal-ri-queued-blacksmith gv-no)
=>
(research ri-forging)
(set-goal goal-ri-queued-blacksmith gv-yes)
(up-chat-data-to-self "Researching forging." c: 0))

(defrule
(current-age == imperial-age)
(can-research ri-pikeman)
=>
(research ri-pikeman)
(up-chat-data-to-self "Researching pikeman." c: 0))

(defrule
(current-age == imperial-age)
(can-research ri-chain-mail)
(goal goal-ri-queued-blacksmith gv-no)
=>
(research ri-chain-mail)
(set-goal goal-ri-queued-blacksmith gv-yes)
(up-chat-data-to-self "Researching chain mail armor." c: 0))

(defrule
(current-age == imperial-age)
(can-research ri-iron-casting)
(goal goal-ri-queued-blacksmith gv-no)
=>
(research ri-iron-casting)
(set-goal goal-ri-queued-blacksmith gv-yes)
(up-chat-data-to-self "Researching iron casting." c: 0))

(defrule
(current-age == imperial-age)
(can-research ri-tracking)
=>
(research ri-tracking)
(up-chat-data-to-self "Researching tracking." c: 0))

(defrule
(current-age == imperial-age)
(can-research ri-squires)
=>
(research ri-squires)
(up-chat-data-to-self "Researching squires." c: 0))

(defrule
(current-age == imperial-age)
(can-research ri-halberdier)
=>
(research ri-halberdier)
(up-chat-data-to-self "Researching halbardier." c: 0))

(defrule
(current-age == imperial-age)
(can-research ri-arson)
=>
(research ri-arson)
(up-chat-data-to-self "Researching arson." c: 0))

(defrule
(current-age == imperial-age)
(can-research ri-conscription)
=>
(research ri-conscription)
(up-chat-data-to-self "Researching conscription." c: 0))

(defrule
(current-age == imperial-age)
(can-research ri-blast-furnace)
(goal goal-ri-queued-blacksmith gv-no)
=>
(research ri-blast-furnace)
(set-goal goal-ri-queued-blacksmith gv-yes)
(up-chat-data-to-self "Researching blast furnace." c: 0))

(defrule
(current-age == imperial-age)
(can-research ri-plate-mail)
(goal goal-ri-queued-blacksmith gv-no)
=>
(research ri-plate-mail)
(set-goal goal-ri-queued-blacksmith gv-yes)
(up-chat-data-to-self "Researching plate mail." c: 0))

(defrule
(current-age == imperial-age)
(can-research ri-chemistry)
(goal goal-ri-queued-university gv-no)
=>
(research ri-chemistry)
(set-goal goal-ri-queued-university gv-yes)
(up-chat-data-to-self "Researching chemistry." c: 0))

(defrule
(current-age == imperial-age)
(can-research ri-caravan)
=>
(research ri-caravan)
(up-chat-data-to-self "Researching caravan." c: 0))

(defrule
(current-age == imperial-age)
(goal gc-civ-has-knights gv-yes)
(can-research ri-chain-barding)
(goal goal-ri-queued-blacksmith gv-no)
=>
(research ri-chain-barding)
(set-goal goal-ri-queued-blacksmith gv-yes)
(up-chat-data-to-self "Researching chain barding." c: 0))

(defrule
(current-age == imperial-age)
(goal gc-civ-has-knights gv-yes)
(can-research ri-scale-barding)
(goal goal-ri-queued-blacksmith gv-no)
=>
(research ri-scale-barding)
(set-goal goal-ri-queued-blacksmith gv-yes)
(up-chat-data-to-self "Researching scale barding." c: 0))

(defrule
(current-age == imperial-age)
(goal gc-civ-has-knights gv-yes)
(can-research ri-plate-barding)
(goal goal-ri-queued-blacksmith gv-no)
=>
(research ri-plate-barding)
(set-goal goal-ri-queued-blacksmith gv-yes)
(up-chat-data-to-self "Researching plate barding." c: 0))

(defrule
(current-age == imperial-age)
(goal gc-civ-has-knights gv-yes)
(can-research ri-bloodlines)
(goal goal-ri-queued-blacksmith gv-no)
=>
(research ri-bloodlines)
(set-goal goal-ri-queued-blacksmith gv-yes)
(up-chat-data-to-self "Researching bloodlines." c: 0))

(defrule
(current-age == imperial-age)
(goal gc-civ-has-knights gv-yes)
(can-research ri-cavalier)
(goal goal-ri-queued-blacksmith gv-no)
=>
(research ri-cavalier)
(set-goal goal-ri-queued-blacksmith gv-yes)
(up-chat-data-to-self "Researching cavalier." c: 0))

(defrule
(current-age == imperial-age)
(goal gc-civ-has-knights gv-yes)
(can-research ri-paladin)
(goal goal-ri-queued-blacksmith gv-no)
=>
(research ri-paladin)
(set-goal goal-ri-queued-blacksmith gv-yes)
(up-chat-data-to-self "Researching paladin." c: 0))

(defrule
(current-age == imperial-age)
(goal gc-civ-has-knights gv-yes)
(can-research ri-husbandry)
(goal goal-ri-queued-blacksmith gv-no)
=>
(research ri-husbandry)
(set-goal goal-ri-queued-blacksmith gv-yes)
(up-chat-data-to-self "Researching husbandry." c: 0))

(defrule
(current-age == imperial-age)
(can-research ri-hand-cart)
=>
(research ri-hand-cart)
(up-chat-data-to-self "Researching hand-cart." c: 0))

(defrule
(current-age == imperial-age)
(can-research ri-town-patrol)
=>
(research ri-town-patrol)
(up-chat-data-to-self "Researching town-patrol." c: 0))

(defrule
(current-age == imperial-age)
(can-research ri-town-watch)
=>
(research ri-town-watch)
(up-chat-data-to-self "Researching town-watch." c: 0))

(defrule
(current-age == imperial-age)
(goal gc-civ-has-bombard-cannons gv-no)
(can-research ri-capped-ram)
=>
(research ri-capped-ram)
(up-chat-data-to-self "Researching capped ram." c: 0))

(defrule
(current-age == imperial-age)
(goal gc-civ-has-bombard-cannons gv-no)
(can-research ri-siege-ram)
=>
(research ri-siege-ram)
(up-chat-data-to-self "Researching siege ram." c: 0))

; -----------------------------------------------------------------------------

; ----- Buildings -------------------------------------------------------------

(defrule
(or (goal goal-dark-rush gv-dr-building)
    (goal goal-dark-rush gv-dr-rushing))
(can-build house)
(housing-headroom < 7)
(population-headroom > 3)
(up-pending-objects c: house < 1) ; this rule will queue otherwise
=>
(build house)
(up-chat-data-to-self "Building house since headroom < 7" c: 0))

(defrule
(or (goal goal-dark-rush gv-dr-finished)
    (goal goal-dark-rush gv-dr-disabled))
(can-build house)
(housing-headroom < 4)
(population-headroom > 3)
(up-pending-objects c: house < 1)
=>
(build house)
(up-chat-data-to-self "Building house since headroom < 4" c: 0))

; ASAP
(defrule
(goal goal-dark-rush gv-dr-building)
(building-type-count-total barracks < 1)
(can-build barracks)
=>
(build barracks)
(up-chat-data-to-self "Building a barracks ASAP for dark age rush." c: 0))

(defrule
(building-type-count-total house > 0)
(building-type-count-total mill == 0)
(resource-found food)
(can-build mill)
=>
(build mill))

(defrule
(building-type-count-total mill > 0)
(building-type-count-total lumber-camp == 0)
(resource-found wood)
(can-build lumber-camp)
=>
(build lumber-camp)
(up-chat-data-to-self "Building first lumber camp." c: 0))

(defrule
(or (goal goal-age-status gv-dark-age)
    (goal goal-age-status gv-advancing-to-feudal))
(goal goal-dark-rush gv-dr-finished)
(idle-farm-count == 0)
(can-build farm)
(up-pending-objects c: farm < 1)
=>
(build farm)
(up-chat-data-to-self "Building a farm now that rush is over" c: 0))

(defrule
(or (current-age > dark-age)
    (goal goal-age-status gv-advancing-to-feudal))
(idle-farm-count == 0)
(up-pending-objects c: farm < 1)
(can-build farm)
=>
(build farm)
(up-chat-data-to-self "Building a farm to satisfy food gatherer percentage" c: 0))

; build another lumber camp once the nearest wood becomes too far away from a dropsite
(defrule
(up-pending-objects c: lumber-camp == 0)
(building-type-count lumber-camp > 0)
(resource-found wood)
(dropsite-min-distance wood > 3)
(dropsite-min-distance wood < 255) ; don't go too far away
; (strategic-number sn-camp-max-distance <= 476) ; max size of map?
(can-build lumber-camp)
=>
; [OFF] (up-modify-sn sn-camp-max-distance c:+ 4)
(build lumber-camp)
(up-chat-data-to-self "Building a lumber camp since wood is more than 5 tiles away." c: 0))
; (up-chat-data-to-self "Increased max camp distance to: %d" s: sn-camp-max-distance))

(defrule
(or (current-age > dark-age)
    (goal goal-age-status gv-advancing-to-feudal))
(resource-found gold)
(building-type-count mining-camp < 4)
(cc-players-unit-type-count 0 66 > 0) ; gold exists on the map
(dropsite-min-distance gold > 5)
(dropsite-min-distance gold < 255)
(up-pending-objects c: mining-camp < 1)
(can-build mining-camp)
=>
(build mining-camp)
(up-chat-data-to-self "Building a mining camp for gold." c: 0))

; ----- Feudal ------------------------

(defrule
(current-age >= feudal-age)
(building-type-count-total blacksmith < 1)
(can-build blacksmith)
=>
(build blacksmith))

(defrule
(current-age == feudal-age)
(building-type-count-total market < 1)
(can-build market)
=>
(build market))

(defrule
(current-age >= feudal-age)
(building-type-count-total blacksmith > 0)
(building-type-count-total market > 0)
(resource-found stone)
(dropsite-min-distance stone > 5)
(dropsite-min-distance stone < 255)
(building-type-count mining-camp < 4)
(can-build mining-camp)
(up-pending-objects c: mining-camp < 1)
=>
(build mining-camp)
(up-chat-data-to-self "Building a mining camp for stone." c: 0))

; ----- Castle ------------------------

(defrule
(can-build castle)
(building-type-count town-center >= 3)
(building-type-count-total castle < 2)
=>
(build castle)
(up-chat-data-to-self "Building a castle since there are 3+ town centers" c: 0))

(defrule
(building-type-count-total town-center < 3)
(can-build town-center)
=>
(build town-center)
(up-chat-data-to-self "Building a town center since count < 3." c: 0))

(defrule
(building-type-count-total town-center < 6)
(can-build town-center)
(civilian-population > 75)
=>
(build town-center)
(up-chat-data-to-self "Building a town center since pop > 75 and TC count < 6." c: 0))

(defrule
(or (current-age == castle-age)
    (current-age == imperial-age))
(building-type-count town-center >= 3)
(can-build house)
(housing-headroom < 20)
(population-headroom > 3)
(up-pending-objects c: house < 2)
=>
(build house)
(up-chat-data-to-self "Building a house since headroom < 20." c: 0))

(defrule
(civilian-population > 100)
(building-type-count-total town-center >= 3)
(building-type-count-total farm < 60)
(can-build farm)
=>
(build farm)
(up-chat-data-to-self "Building at least 60 farms." c: 0))

; ----- Imperial ----------------------

(defrule
(current-age == imperial-age)
(building-type-count-total university < 1)
(can-build university)
=>
(build university)
(up-chat-data-to-self "Building a university." c: 0))

(defrule
(goal goal-flood-spearmen gv-yes)
(current-age == imperial-age)
(building-type-count-total barracks < 10)
(can-build barracks)
=>
(build barracks)
(up-chat-data-to-self "Need to flood spearmen. Building a barracks since count < 10" c: 0))

(defrule
(goal goal-flood-spearmen gv-yes)
(current-age == castle-age)
(building-type-count-total barracks < 4)
(can-build barracks)
=>
(build barracks)
(up-chat-data-to-self "Need to flood spearmen. Building a barracks since count < 4" c: 0))

; May need to balance creation order btwn these and barracks
(defrule
(goal gc-civ-has-knights gv-yes)
(current-age == imperial-age)
(building-type-count-total stable < 3)
(can-build stable)
=>
(build stable)
(up-chat-data-to-self "Building a stable since count < 3" c: 0))

(defrule
(current-age == imperial-age)
(building-type-count-total siege-workshop < 2)
(can-build siege-workshop)
=>
(build siege-workshop)
(up-chat-data-to-self "Building a siege workshop since count < 2" c: 0))

; [OFF] currently broken
;(defrule
;(current-age == imperial-age)
;(building-type-count-total castle >= 2)
;(goal goal-army-status gv-army-status-attacking)
;(can-build castle)
;=>
;(build-forward castle)
;(up-chat-data-to-self "Castle rule 2 (forward)" c: 0))

(defrule
(current-age == imperial-age)
=>
(set-strategic-number sn-camp-max-distance 360)
(disable-self)
(up-chat-data-to-self "Now in Imperial Age, setting camp distance to 360" c: 0))

(defrule
(building-type-count farm > 15)
(building-type-count-total mill < 2)
(can-build mill)
=>
(build mill)
(up-chat-data-to-self "Building a mill since count < 2 and farms count > 15" c: 0))

(defrule
(building-type-count farm > 25)
(building-type-count-total mill < 3)
(can-build mill)
=>
(build mill)
(up-chat-data-to-self "Building a mill since count < 3 and farms count > 25" c: 0))

(defrule
(building-type-count farm > 30)
(building-type-count-total mill < 4)
(can-build mill)
=>
(build mill)
(up-chat-data-to-self "Building a mill since count < 4 and farms count > 30" c: 0))

; -----------------------------------------------------------------------------

; ----- Unit training ---------------------------------------------------------

(defrule
(current-age == dark-age)
(or (goal goal-dark-rush gv-dr-building)
    (goal goal-dark-rush gv-dr-rushing))
(unit-type-count-total villager < dark-age-rush-max-villagers)
(can-train villager)
=>
(train villager))

; constantly train villagers
(defrule
(or (goal goal-dark-rush gv-dr-finished)
    (goal goal-dark-rush gv-dr-disabled))
(unit-type-count-total villager < 200)
(can-train villager)
=>
(train villager))

(defrule
(or (goal goal-dark-rush gv-dr-building)
    (goal goal-dark-rush gv-dr-rushing))
(unit-type-count-total militiaman-line >= end-dark-rush-militia-count)
=>
(set-goal goal-max-militia-reached gv-yes)
(up-chat-data-to-self "Done training rush militia." c: 0)
(disable-self))

(defrule
(or (goal goal-dark-rush gv-dr-building)
    (goal goal-dark-rush gv-dr-rushing))
(goal goal-max-militia-reached gv-no)
(can-train militiaman-line)
=>
(train militiaman-line))

(defrule
(goal goal-dark-rush-forward-build gv-drbf-built)
(can-train militiaman-line)
=>
(train militiaman-line))

; -------------------------------------

(defrule
(current-age == imperial-age)
(civilian-population > 80)
(goal goal-army-composition gv-army-comp-defensive)
(goal goal-army-status gv-army-status-idle)
=>
(set-goal goal-army-composition gv-army-comp-offensive)
(up-chat-data-to-self "Building an attack force since civilian pop > 80 and army is idle." c: 0))

(defrule
(goal goal-army-composition gv-army-comp-offensive)
(goal goal-army-status gv-army-status-defending)
=>
(set-goal goal-army-composition gv-army-comp-defensive)
(up-chat-data-to-self "Reverting to defensive army composition" c: 0))

(defrule
(goal goal-army-composition gv-army-comp-offensive)
=>
(set-goal goal-flood-spearmen gv-yes))

(defrule
(goal goal-flood-spearmen gv-yes)
(unit-type-count-total spearman-line < 150) ; crude throttle, need a priority system
(can-train spearman-line)
=>
(train spearman-line))

(defrule
(goal goal-army-composition gv-army-comp-offensive)
(goal gc-civ-has-bombard-cannons gv-no)
; What if we're out of gold?
(unit-type-count-total battering-ram-line < 10)
(can-train battering-ram-line)
=>
(train battering-ram-line))

(defrule
(or (goal goal-army-composition gv-army-comp-offensive)
    (civilian-population > 100))
(can-train knight-line)
=>
(train knight-line))

(defrule
(or (goal goal-army-composition gv-army-comp-offensive)
    (civilian-population > 125))
(can-train bombard-cannon)
=>
(train bombard-cannon))

(defrule
(current-age == imperial-age)
(player-in-game any-ally)
(cc-players-building-type-count any-ally market > 0)
(unit-type-count-total trade-cart < 30)
(can-train trade-cart)
=>
(train trade-cart))

; -----------------------------------------------------------------------------

; ----- Attack rules ----------------------------------------------------------

; ----- Dark Age Rush -----------------

; re-issue the attack now call every minute

(defrule
(goal goal-dark-rush gv-dr-rushing)
=>
(enable-timer dark-rush-timer 1)
(disable-self))

(defrule
(timer-triggered dark-rush-timer)
(goal goal-dark-rush gv-dr-rushing)
=>
(attack-now)
(disable-timer dark-rush-timer)
(enable-timer dark-rush-timer 60)
(up-chat-data-to-self "Attack now!" c: 0))

; -------------------------------------

(defrule
(current-age == imperial-age)
(military-population > 75)
(goal goal-army-status gv-army-status-idle)
=>
(set-goal goal-army-status gv-army-status-attacking)
(set-goal goal-tsa gv-yes)
(set-strategic-number sn-enemy-sighted-response-distance 2)
(up-chat-data-to-self "Military pop > 75 and army is idle. Begin the siege!" c: 0))

(defrule
(goal goal-tsa gv-yes)
(not(enemy-buildings-in-town))
=>
(up-modify-sn sn-maximum-town-size c:+ 2)
(up-chat-data-to-self  "town size: %d" s: sn-maximum-town-size))

(defrule
(goal goal-tsa gv-yes)
(enemy-buildings-in-town)
=>
(set-goal goal-tsa gv-no)
(up-chat-data-to-self "Maxed out TSA. Attack!" c: 0))

(defrule
(goal goal-tsa gv-no)
(not (enemy-buildings-in-town))
(goal goal-army-status gv-army-status-attacking)
=>
(set-goal goal-tsa gv-yes)
(up-chat-data-to-self "Press further!" c: 0))

(defrule
;(or (military-population < 20)
;    (town-under-attack)) ;; this resets the TSA too early
(military-population < 20)
(goal goal-army-status gv-army-status-attacking)
=>
(up-retreat-now)
(set-goal goal-tsa gv-no)
(set-goal goal-army-status gv-army-status-defending)
(set-strategic-number sn-maximum-town-size 30)
(up-chat-data-to-self "Fall back!" c: 0)
(up-chat-data-to-self  "town size: %d" s: sn-maximum-town-size))

; -----------------------------------------------------------------------------

; ----- Defend rules ----------------------------------------------------------

(defrule
(not (town-under-attack))
(not (enemy-buildings-in-town))
(goal goal-army-status gv-army-status-defending)
=>
(set-goal goal-flood-spearmen gv-no)
(set-goal goal-army-status gv-army-status-idle)
(set-strategic-number sn-enemy-sighted-response-distance 25)
(up-chat-data-to-self "Army is now idle" c: 0))

(defrule
(or (current-age == castle-age)
    (current-age == imperial-age))
(town-under-attack)
(goal goal-army-status gv-army-status-idle)
=>
(set-goal goal-army-status gv-army-status-defending)
(set-goal goal-flood-spearmen gv-yes)
(set-strategic-number sn-enemy-sighted-response-distance 144)
(enable-timer generic-defend-timer 30)
(up-chat-data-to-self "We're under attack!" c: 0))

(defrule
(timer-triggered generic-defend-timer)
(not (town-under-attack))
=>
(disable-timer generic-defend-timer)
(up-chat-data-to-self "No longer under attack" c: 0))

(defrule
(timer-triggered generic-defend-timer)
(town-under-attack)
=>
(disable-timer generic-defend-timer)
(enable-timer generic-defend-timer 30)
(up-chat-data-to-self "Still under attack" c: 0))

; -----------------------------------------------------------------------------

; ----- Town Size -------------------------------------------------------------

(defrule
(current-age == imperial-age)
=>
(enable-timer increase-town-size-timer 1)
(disable-self))

(defrule
(timer-triggered increase-town-size-timer)
(strategic-number sn-maximum-town-size < max-town-size)
=>
(up-modify-sn sn-maximum-town-size c:+ 2)
(disable-timer increase-town-size-timer)
(enable-timer increase-town-size-timer 30)
(up-chat-data-to-self  "town size: %d" s: sn-maximum-town-size))

(defrule
(timer-triggered increase-town-size-timer)
(strategic-number sn-maximum-town-size >= max-town-size)
=>
(disable-timer increase-town-size-timer))

(defrule
(current-age == castle-age)
(civilian-population > 100)
=>
(up-modify-sn sn-maximum-town-size c:+ 10)
(up-chat-data-to-self "Town size increased since pop > 100. New size : %d" s: sn-maximum-town-size)
(disable-self))

; -----------------------------------------------------------------------------

; ----- Market rules ----------------------------------------------------------

(defrule
(current-age == imperial-age)
(wood-amount > 8000)
=>
(sell-commodity wood)
(up-chat-data-to-self "selling excess wood" c: 0))

(defrule
(current-age == imperial-age)
(food-amount > 8000)
=>
(sell-commodity food)
(up-chat-data-to-self "selling excess food" c: 0))

; -----------------------------------------------------------------------------