
; Conditions to remember:
; High starting resources
; High population limit (500)
; Should build villagers and militia in parallel and attack!

; ----- Constants -------------------------------------------------------------

(defconst max-num-dark-villagers 30)

(defconst scouting-timer 3)

(defconst goal-dark-rush 1)

(defconst start-dark-rush-militia-count 10)
(defconst end-dark-rush-militia-count 15)

(defconst gv-dr-building 1) ; dark rush states
(defconst gv-dr-rushing 2)
(defconst gv-dr-finished 3)
(defconst gv-on 1)
(defconst gv-off 0)

; Initial state
(defrule
(true)
=>
(set-goal goal-dark-rush gv-dr-building)
(disable-self))

; -----------------------------------------------------------------------------

; ----- Strategic Numbers -----------------------------------------------------

(defrule
(true)
=>
(up-send-scout group-type-land-explore scout-opposite)
(chat-local-to-self "Sending scout to opposite side")
(disable-self))

; All units scout until the first berries or sheep are found.

;(defrule
;(true)
;=>
;(set-strategic-number sn-percent-civilian-explorers 100)
;(set-strategic-number sn-minimum-civilian-explorers 100)
;(set-strategic-number sn-cap-civilian-explorers 100)
;(set-strategic-number sn-total-number-explorers 101)
;(set-strategic-number sn-number-explore-groups 101)
;(set-strategic-number sn-minimum-explore-group-size 1)
;(set-strategic-number sn-maximum-explore-group-size 1)
;(set-strategic-number sn-initial-exploration-required 0)
;(set-strategic-number sn-percent-attack-soldiers 100)
;(set-strategic-number sn-blot-exploration-map 0)
;(set-strategic-number sn-home-exploration-time 0)
;(disable-self))

(defrule
;(resource-found food)
(true)
=>
(set-strategic-number sn-percent-civilian-explorers 0)
(set-strategic-number sn-minimum-civilian-explorers 0)
(set-strategic-number sn-cap-civilian-explorers 0)
(set-strategic-number sn-total-number-explorers 1)
(set-strategic-number sn-number-explore-groups 1)
(disable-self))

(defrule
(true)
=>
(chat-local-to-self "Searching for the closest enemy.")
(up-send-scout group-type-land-explore position-opposite)
(enable-timer scouting-timer 5)
(disable-self))

(defrule
(timer-triggered scouting-timer)
(game-time < 1200)
(players-building-count every-enemy <= 0)
(military-population >= 1)
=>
(chat-local-to-self "Searching for the closest enemy again.")
(up-send-scout group-type-land-explore position-opposite)
(enable-timer scouting-timer 5)); 80

(defrule
(goal goal-dark-rush gv-dr-building)
=>
; Unit heavy in dark age, prioritize food
(set-strategic-number sn-food-gatherer-percentage 80)
(set-strategic-number sn-wood-gatherer-percentage 20)
(set-strategic-number sn-gold-gatherer-percentage 0)
(set-strategic-number sn-stone-gatherer-percentage 0))

; -----------------------------------------------------------------------------

; ----- Age Advancement -------------------------------------------------------

(defrule
(goal goal-dark-rush gv-dr-finished)
(can-research feudal-age)
=>
(research feudal-age)
(chat-local-to-self "Heading to Feudal!"))

; -----------------------------------------------------------------------------

; ----- Dark Age Rush ---------------------------------------------------------

(defrule
(goal goal-dark-rush gv-dr-building)
(unit-type-count-total militiaman-line > start-dark-rush-militia-count)
=>
; attack will begin 60 seconds after
(set-goal goal-dark-rush gv-dr-rushing)
(chat-local-to-self "Rushing!"))

(defrule
(goal goal-dark-rush gv-dr-rushing)
(unit-type-count-total militiaman-line > end-dark-rush-militia-count)
=>
(set-goal goal-dark-rush gv-dr-finished)
(chat-local-to-self "Finished rushing."))

; -----------------------------------------------------------------------------

; ----- Buildings -------------------------------------------------------------

; ASAP
(defrule
(building-type-count-total barracks < 2)
(can-build barracks)
=>
(build barracks))

(defrule
(or (goal goal-dark-rush gv-dr-building)
    (goal goal-dark-rush gv-dr-rushing))
(can-build house)
(housing-headroom < 7)
(population-headroom > 3)
=>
(build house))

; default - fallback once rush is over
(defrule
(can-build house)
(housing-headroom < 4)
(population-headroom > 3)
=>
(build house))

; [OFF] Research loom ASAP
;(defrule
;(can-research ri-loom)
;=>
;(research ri-loom)
;(chat-local-to-self "Researching loom."))

(defrule
(building-type-count-total house > 0)
(building-type-count-total mill == 0)
(resource-found food)
(can-build mill)
=>
(build mill))

(defrule
(building-type-count-total mill > 0)
(building-type-count-total lumber-camp == 0)
(resource-found wood)
(can-build lumber-camp)
=>
(build lumber-camp))

; we have enough food (+sheep, +berries) for the initial
; rush, but once in action we MUST push to Feudal and roar
; the economy
(defrule
(or (goal goal-dark-rush gv-dr-rushing)
    (goal goal-dark-rush gv-dr-finished))
(building-type-count-total mill > 0)
(building-type-count-total lumber-camp > 0)
(or (building-type-count-total farm < 15)
    (idle-farm-count == 0))
(can-build farm)
=>
(build farm))

; -----------------------------------------------------------------------------

; ----- Unit training ---------------------------------------------------------

(defrule
(current-age == dark-age)
(unit-type-count-total villager < max-num-dark-villagers)
(can-train villager)
=>
(train villager))

; constantly train villagers
(defrule
(not (current-age == dark-age))
(can-train villager)
=>
(train villager))

(defrule
(or (goal goal-dark-rush gv-dr-building)
    (goal goal-dark-rush gv-dr-rushing))
(can-train militiaman-line)
=>
(train militiaman-line))

; -----------------------------------------------------------------------------

; ----- Attack rules ----------------------------------------------------------

; re-issue the attack now call every minute

(defrule
(unit-type-count-total militiaman-line > start-dark-rush-militia-count)
=>
(enable-timer 1 60)
(chat-local-to-self "Attacking in 60 seconds.")
(disable-self))

(defrule
(timer-triggered 1)
=>
(attack-now)
(disable-timer 1)
(enable-timer 1 60)
(chat-local-to-self "Attack now!"))

; -----------------------------------------------------------------------------