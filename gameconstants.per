; This file contains all evaluations that can be done at the beginning of the game.
; Only evalated once since they won't change.

; Some races have spotty support in built-in values and need to manage differently.

#load-if-defined INCAN-CIV
(defrule
(true)
=>
(set-goal gc-is-incas gv-yes)
(disable-self))
#else
(defrule
(true)
=>
(set-goal gc-is-incas gv-no)
(disable-self))
#end-if

#load-if-defined INDIAN-CIV
(defrule
(true)
=>
(set-goal gc-is-indians gv-yes)
(disable-self))
#else
(defrule
(true)
=>
(set-goal gc-is-indians gv-no)
(disable-self))
#end-if

(defrule
(or
    (or (civ-selected aztec)
        (goal gc-is-incas gv-yes))
    (or (goal gc-is-indians gv-yes)
        (civ-selected mayan)))
=>
(set-goal gc-civ-has-knights gv-no)
(up-chat-data-to-self "Fact: Civ can NOT build knights." c: 0)
(disable-self))

(defrule
(not (goal gc-civ-has-knights gv-no))
=>
(set-goal gc-civ-has-knights gv-yes)
(up-chat-data-to-self "Fact: Civ can build knights." c: 0)
(disable-self))

;;

(defrule
; fun!
(or (or (or (or (civ-selected aztec)
                (civ-selected briton))
            (or (civ-selected celtic)
                (civ-selected chinese)))
        (or (or (civ-selected hun)
                (goal gc-is-incas gv-yes))
            (or (civ-selected japanese)
                (civ-selected magyar))))
    (or (or (or (civ-selected mayan)
                (civ-selected mongol))
            (or (civ-selected slavic)
                (civ-selected viking)))
        (false)))
=>
(set-goal gc-civ-has-bombard-cannons gv-no)
(up-chat-data-to-self "Fact: Civ can NOT build bombard cannons." c: 0)
(disable-self))

(defrule
(not (goal gc-civ-has-bombard-cannons gv-no))
=>
(set-goal gc-civ-has-bombard-cannons gv-yes)
(up-chat-data-to-self "Fact: Civ can build bombard cannons." c: 0)
(disable-self))

; Determine if playing against any computer players
(defrule
(player-computer any-enemy)
(not (player-computer every-enemy))
=>
(set-goal gc-some-enemy-is-ai gv-yes)
(set-goal gc-every-enemy-is-ai gv-no)
(up-chat-data-to-self "Fact: Some of the enemies are AI." c: 0)
(disable-self))

(defrule
(player-computer every-enemy)
=>
(set-goal gc-some-enemy-is-ai gv-no)
(set-goal gc-every-enemy-is-ai gv-yes)
(up-chat-data-to-self "Fact: Every enemy is AI." c: 0)
(disable-self))

(defrule
(not (player-computer any-enemy))
=>
(set-goal gc-some-enemy-is-ai gv-no)
(set-goal gc-every-enemy-is-ai gv-no)
(up-chat-data-to-self "Fact: Every enemy is human." c: 0))